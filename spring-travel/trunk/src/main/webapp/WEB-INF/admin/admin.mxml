<?xml version="1.0" encoding="utf-8"?>
<mx:Application 
	xmlns:mx="http://www.adobe.com/2006/mxml" 
	creationComplete="init()">
	
	<mx:Script>
		<![CDATA[
		
		import mx.collections.ArrayCollection;
		import mx.messaging.ChannelSet;
	  	import mx.messaging.channels.AMFChannel;
	  	import mx.rpc.events.ResultEvent;
	    import mx.rpc.events.FaultEvent;  	
	  	import mx.rpc.remoting.RemoteObject;
	  	import mx.controls.Alert;
	  	import mx.messaging.events.MessageEvent;
		import mx.messaging.Consumer;
		
		private var bookings:ArrayCollection;
		
		private var travelService:RemoteObject = new RemoteObject();
		
		private function init():void {
			travelService.channelSet = cs;
			travelService.destination = "travelService";
			
			travelService.findBookings.addEventListener("result", resultHandler);
			travelService.addEventListener("fault", faultHandler);
			
			travelService.findBookings("jeremy");
			
			subscribe();
		}
		
		private function subscribe():void
		{
			var consumer:Consumer = new Consumer();
			consumer.destination = "bookings";
			consumer.channelSet = cs;
			consumer.addEventListener(MessageEvent.MESSAGE, messageHandler);
			consumer.subscribe();
		}
		
		private function messageHandler(event:MessageEvent):void 
		{
			dg.dataProvider.addItem(event.message.body);
        }
		
		private function resultHandler(event:ResultEvent):void {
			// Make sure that the result can be cast to the correct type.
			if (event.result is ArrayCollection)
			{
			    // Cast the result to the correct type.
			    dg.dataProvider=event.result as ArrayCollection;
			}
			else {
			    dg.dataProvider = null;
			}
		}
		
		private function faultHandler(event:FaultEvent):void {
			Alert.show("Travel Service Failure: "+event.toString());
		}
		
		private function hotelName(item:Object, column:DataGridColumn):String {
			return item.hotel.name.toString();
		}
		
		]]>
	</mx:Script>
	
	<mx:ChannelSet id="cs">
		<mx:AMFChannel url="http://localhost:8080/spring-3-travel/messagebroker/amflongpolling"/>
		<mx:AMFChannel url="http://localhost:8080/spring-3-travel/messagebroker/amfpolling"/>
	</mx:ChannelSet>
	
	<mx:DataGrid id="dg" width="100%" height="100%"
		doubleClickEnabled="true">
		<mx:columns>
			<mx:DataGridColumn labelFunction="hotelName" headerText="Hotel"/>
			<mx:DataGridColumn dataField="checkinDate" headerText="Check In"/>
			<mx:DataGridColumn dataField="checkoutDate" headerText="Check Out"/>
		</mx:columns>
	</mx:DataGrid>

	
</mx:Application>